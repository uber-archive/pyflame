AC_PREREQ([2.68])
AC_INIT([pyflame], [1.5.4], [evan@eklitzke.org])
AC_CONFIG_AUX_DIR([build-aux])
AC_CONFIG_HEADERS([src/config.h])
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_SRCDIR([src/pyflame.cc])

# Fail early if the user tries to build for BSD/OS X
AC_CANONICAL_HOST
AS_CASE([$host_os],
        [linux-gnu*], [],
        [AC_MSG_ERROR([Pyflame can only be built for Linux hosts])])

AS_IF([test x"$host_cpu" = xx86_64],
      [AC_MSG_NOTICE([x86-64 system, threads will be supported])
       AC_DEFINE([ENABLE_THREADS], [1], [Threads are enabled.])
       AC_DEFINE([USE_ELF64], [1], [Expect 64-bit ELF symbols.])
      ],
      [AC_MSG_NOTICE([Threading support will be disabled (only works on x86-64)])
       AC_DEFINE([ENABLE_THREADS], [0], [Threads are enabled.])
       AC_DEFINE([USE_ELF64], [0], [Expect 64-bit ELF symbols.])
      ])

AC_DEFINE_UNQUOTED([HOST_CPU], ["$host_cpu"], [CPU of target architecture.])

AM_INIT_AUTOMAKE([dist-bzip2 foreign subdir-objects -Wall -Werror])

# Checks for programs.
AC_PROG_CXX
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

AM_PROG_AR

LT_INIT

# Ensure Linux kernel headers are present
AC_CHECK_HEADERS([linux/ptrace.h], [], [AC_MSG_ERROR([Linux kernel headers missing])])

AC_LANG([C++])

# This is commented out because it doesn't work right on Ubuntu 12.04 (Precise)
# AX_CXX_COMPILE_STDCXX_11
AX_CHECK_COMPILE_FLAG(["-std=c++11"],
                      [AX_APPEND_FLAG(["-std=c++11"], [CXXFLAGS])],
                      [AX_CHECK_COMPILE_FLAG(["-std=c++0x"],
                                             [AX_APPEND_FLAG(["-std=c++0x"], [CXXFLAGS])],
                                             [AC_MSG_ERROR([failed to detect C++11 support])])])

AX_CHECK_COMPILE_FLAG([-Wall],
                      [AX_APPEND_FLAG([-Wall], [CXXFLAGS])])

# Checks for libraries.

# Checks for header files.
AC_CHECK_HEADERS([fcntl.h limits.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_CHECK_HEADER_STDBOOL
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_SSIZE_T
AC_TYPE_UINT16_T
AC_TYPE_UINT8_T

# Checks for library functions.
AC_FUNC_FORK
AC_FUNC_LSTAT_FOLLOWS_SLASHED_SYMLINK
AC_FUNC_MMAP
AC_CHECK_FUNCS([getpagesize memmove munmap strerror strtol strtoul])

AC_DEFINE_UNQUOTED(
  [PYFLAME_VERSION_STR],
  ["Pyflame $PACKAGE_VERSION (commit `cd $srcdir && git describe --always`) $host_os $host_cpu"],
  [A string containing build information.])

PKG_CHECK_MODULES([PY26], [python2], [enable_py26="yes"], [AC_MSG_WARN([Building without Python 2.6/2.7 support])])
AM_CONDITIONAL([ENABLE_PY26], [test x"$enable_py26" = xyes])
AM_COND_IF([ENABLE_PY26], [AC_DEFINE([ENABLE_PY26], [1], [Python 2.6/2.7 will be enabled])])

PKG_CHECK_MODULES([PY34], [python-3.4], [enable_py34="yes"],
                          [PKG_CHECK_MODULES([PY34], [python-3.5], [enable_py34="yes"], [AC_MSG_WARN([Building without Python 3.4/3.5 support])])])
AM_CONDITIONAL([ENABLE_PY34], [test x"$enable_py34" = xyes])
AM_COND_IF([ENABLE_PY34], [AC_DEFINE([ENABLE_PY34], [1], [Python 3.4/3.5 will be enabled])])

PKG_CHECK_MODULES([PY36], [python-3.6], [enable_py36="yes"], [AC_MSG_WARN([Building without Python 3.6 support])])
AM_CONDITIONAL([ENABLE_PY36], [test x"$enable_py36" = xyes])
AM_COND_IF([ENABLE_PY36], [AC_DEFINE([ENABLE_PY36], [1], [Python 3.6 will be enabled])])

AS_IF([test x"$enable_py26" = xyes -o x"$enable_py34" = xyes -o x"$enable_py36" = xyes],
      [AC_MSG_NOTICE([Found at least one copy of Python.h])],
      [AC_MSG_ERROR([Failed to find a supported Python.h])]
)

AC_CONFIG_FILES([Makefile
                 src/Makefile])
AC_OUTPUT
